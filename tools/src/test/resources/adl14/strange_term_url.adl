archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.drugallergy.v1

concept
	[at0000]	-- Drug allergy
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2012-09-12">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of a known or suspected allergy or other adverse reaction, intolerance to a drug or medicinal substance, drug group or trade family group of drugs.">
			use = <"This archetype is very closely aligned to the CfH GP2GP Drug allergy archetype and should be used to record allergies, intolerances and adverse reactions to drugs and other medicinal substances.  It is designed primarily for use with in a UK primary care context but may be suitable for use within secondary care, particularly where the prime requirement is to record propensity to drug allergy for decision support purposes.">
			keywords = <"allergy", "adverse", "intolerance">
			misuse = <"This archetype should not be used to record adverse reactions or allergies to non-medicinal substances such as foodstuffs, pollens or animals.">
			copyright = <"Â© openEHR Foundation">
		>
	>
	lifecycle_state = <"Initial">
	other_contributors = <"Leo Fogarty, GP2GP project, NHS England", "John Williams, GP2GP project, NHS England">
	other_details = <
		["references"] = <"NHS Scotland SCI-XML schema http://www.sci.scot.nhs.uk/products/gateway/gate_down_xml.htm

DoH, UK  NHS Message Implementation Guidance, http://www.uktcregistration.nss.cfh.nhs.uk/trud3/user/guest/group/0/pack/23;jsessionid=D52317DAD88F60096DFF3113BA604F1A">
		["MD5-CAM-1.0.1"] = <"FBD57278DBCC612BF8FF04A47B972710">
	>

definition
	EVALUATION[at0000] matches {	-- Drug allergy
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0002] matches {	-- Causative agent
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[ac0002]}		-- CausativeAgent
							}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Reaction
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {[ac0001]}		-- Clinical Finding
							}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Certainty
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0015, 	-- Unlikely
									at0017, 	-- Likely
									at0020, 	-- Certain
									at0018]	-- Confirmed
								}
							}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Severity
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0009, 	-- Mild
									at0012, 	-- Mild to moderate
									at0010, 	-- Moderate
									at0013, 	-- Moderate to severe
									at0011, 	-- Severe
									at0014]	-- Life-threatening
								}
							}
						}
					}
					ELEMENT[at0021] occurrences matches {0..1} matches {	-- Date of onset
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0023] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0024] occurrences matches {0..1} matches {	-- Record provenance
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.record_provenance(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}

ontology
	terminologies_available = <"DM+D", "SNOMED", "SNOMED-CT", "READ", "FDB", "EMIS_DRUGS">
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Drug allergy">
					description = <"Details of an an allergy or other adverse reaction to a drug or other medicinal substance.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Causative agent">
					description = <"Details of the agent or medicinal substance believed to be the cause of the adverse reaction or allergy.">
					comment = <"For GP2GP V2 use, there are 4 permissible ways to express a Causative agent:
Drug Group (SNOMED-CT),
dm+d VMP/AMP (dm+d),
Ingredient (SNOMED-CT),
 TradeFamily/TradeFamilyGroup (SNOMED-CT).
It is MANDATORY that one and only one of these must always be selected. Optionally, in addition a SNOMED AllergyCode may be sent as a mapping, but can never be sent on its own.
For legacy use other codes are permissible such as FDB Agent codes, EMIS Drug codes and READ codes.">
				>
				["at0003"] = <
					text = <"Certainty">
					description = <"The certainty with which the reaction is deemed to be be due to allergy to the agent. This item has a Translation Set= Read Code (V2 or CTV3) and Snomed Code to ensure forwards and backwards compatability.">
				>
				["at0004"] = <
					text = <"Reaction">
					description = <"An optional pre-coodinated unqualified SNOMED-CT code for the nature of the reaction produced by the  drug allergy.">
				>
				["at0005"] = <
					text = <"Severity">
					description = <"The severity of the reaction.">
					comment = <"This item has a Translation Set = Read Code (V2 or CTV3) and Snomed Code to ensure forwards and backwards compatability.">
				>
				["at0009"] = <
					text = <"Mild">
					description = <"The reaction was mild.">
				>
				["at0010"] = <
					text = <"Moderate">
					description = <"The reaction was moderate.">
				>
				["at0011"] = <
					text = <"Severe">
					description = <"The reaction was severe.">
				>
				["at0012"] = <
					text = <"Mild to moderate">
					description = <"The reaction was mild to moderate.">
				>
				["at0013"] = <
					text = <"Moderate to severe">
					description = <"The reaction was moderate to severe.">
				>
				["at0014"] = <
					text = <"Life-threatening">
					description = <"The reaction was life-threatening.">
				>
				["at0015"] = <
					text = <"Unlikely">
					description = <"The reaction was unlikely to have been caused by the agent.">
				>
				["at0017"] = <
					text = <"Likely">
					description = <"The reaction was likely to have been caused by the agent.">
				>
				["at0018"] = <
					text = <"Confirmed">
					description = <"The rection to the agent has been confirmed by testing.">
				>
				["at0020"] = <
					text = <"Certain">
					description = <"The agent was thought to have been certain to have caused the reaction but has not been confirmed by testing.">
				>
				["at0021"] = <
					text = <"Date of onset">
					description = <"The date of onset of the reaction.">
					comment = <"From Welsh IHR model">
				>
				["at0022"] = <
					text = <"Comment">
					description = <"Any additional comment about the allergy.">
					comment = <"From Welsh IHR model">
				>
				["at0023"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0024"] = <
					text = <"Record provenance">
					description = <"Details of the provenance of the record e.g. source, name and role of author, care setting.">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0001"] = <
					text = <"Clinical Finding">
					description = <"Any SNOMED-CT term in the Clinical Finding hierarchy.">
				>
				["ac0002"] = <
					text = <"CausativeAgent">
					description = <"A causative agent term from one of the approved terminology subsets.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0000"] = <[SNOMED-CT::419511003]>
				["at0002"] = <[SNOMED-CT::246075003]>
				["at0003"] = <[SNOMED-CT::255544004]>
				["at0004"] = <[SNOMED-CT::282100009]>
				["at0005"] = <[SNOMED-CT::272141005]>
				["at0009"] = <[SNOMED-CT::255604002]>
				["at0010"] = <[SNOMED-CT::6736007]>
				["at0011"] = <[SNOMED-CT::24484000]>
				["at0012"] = <[SNOMED-CT::255604002]>
				["at0013"] = <[SNOMED-CT::371924009]>
				["at0014"] = <[SNOMED-CT::442452003]>
				["at0015"] = <[SNOMED-CT::1491118016]>
				["at0017"] = <[SNOMED-CT::5961011]>
				["at0018"] = <[SNOMED-CT::410605003]>
				["at0020"] = <[SNOMED-CT::255545003]>
			>
		>
	>
	constraint_bindings = <
		["DM+D"] = <
			items = <
				["ac0002"] = <terminology:DM%2BD?subset=VMP%2FAMP>
			>
		>
		["READ"] = <
			items = <
				["ac0002"] = <terminology:READ?subset=Allergy%20code>
			>
		>
		["SNOMED-CT"] = <
			items = <
				["ac0001"] = <terminology:SNOMED-CT?subset=ClinicalFinding>
				["ac0002"] = <terminology:SNOMED-CT?subset=Allergy%20code%3B%20dm%2Bd%20Ingredient%3B%20Trade%20family%20%3B%20Trade%20Family%20Group>
			>
		>
		["FDB"] = <
			items = <
				["ac0002"] = <terminology:FDB?subset=Agent>
			>
		>
		["EMIS_DRUGS"] = <
			items = <
				["ac0002"] = <terminology:EMIS_DRUGS?subset=Agent>
			>
		>
	>
