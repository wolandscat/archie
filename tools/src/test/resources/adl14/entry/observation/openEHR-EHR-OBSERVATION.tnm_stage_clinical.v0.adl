archetype (adl_version=1.4; uid=8a632df1-78f1-4029-96d1-fb936c27e9dd)
	openEHR-EHR-OBSERVATION.tnm_stage_clinical.v0

concept
	[at0000]	-- TNM stage - clinical
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2016-08-26">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the pretreatment clinical classification and grading of malignancies using the TNM system.">
			use = <"Use to record the clinical classification and grading of malignancies, and designated as TNM or cTNM. This classification is based on evidence acquired before treatment, including physical examination, imaging, endoscopy, biopsy, surgical exploration and other relevant examinations. It is used as the basis for the choice of treatment.

Each cancer has a set of unique TNM classification values. It is expected that this archetype will be further constrained to reflect the unique requirements for each tumour and edition of the TNM classification, using either an archetype specialisation or a template.">
			keywords = <"TNM, cancer, tumour", ...>
			misuse = <"Not to be used for pathological TNM classification.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Hildegard Franke, freshEHR Clinical Informatics Ltd. UK", "Ian McNicoll, freshEHR Clinical Informatics Ltd. UK">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Brierley JD, Gospodarowicz MK, Wittekind C. TNM Classification of Malignant Tumours, 8th Edition. Wiley-Springer; 2016. 272 p.

Principles of Cancer Staging. AJCC American Joint Committee on Cancer; [cited 2019 03 15]. Available at: https://facs.groupdropbox.com/share/ac03e57b3ea913ab7d728f5a9f621e84/download?file_ids[]=13911553.

TNM Classification Help (Manual for Cancer Staging); [cited 2019 10 04].  Available at: http://cancerstaging.blogspot.com/.">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, heather.leslie@atomicainformatics.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"39F419710DF936751F31431B5142192F">
		["build_uid"] = <"e68a2034-aebc-4800-8a16-6c4f973c5d23">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- TNM stage - clinical
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {    -- Point in time event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0005] occurrences matches {0..1} matches {    -- Cancer type
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {    -- Anatomical site
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- Anatomical subsite
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0008] occurrences matches {0..2} matches {    -- Primary tumour (T)
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0024,    -- C1
													at0025,    -- C2
													at0026,    -- C3
													at0027,    -- C4
													at0028]    -- C5
												}
											}
										}
									}
									ELEMENT[at0009] occurrences matches {0..6} matches {    -- Regional lymph nodes (N)
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0024,    -- C1
													at0025,    -- C2
													at0026,    -- C3
													at0027,    -- C4
													at0028]    -- C5
												}
											}
										}
									}
									ELEMENT[at0010] occurrences matches {0..3} matches {    -- Distant metastasis (M)
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0024,    -- C1
													at0025,    -- C2
													at0026,    -- C3
													at0027,    -- C4
													at0028]    -- C5
												}
											}
										}
									}
									ELEMENT[at0011] occurrences matches {0..1} matches {    -- Histopathological grading (G)
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0031,    -- GX
													at0032,    -- G1
													at0033,    -- G2
													at0034,    -- G3
													at0035]    -- G4
												}
											}
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0012] occurrences matches {0..1} matches {    -- Residual tumour (R)
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0047,    -- RX
													at0048,    -- R0
													at0049,    -- R1
													at0050]    -- R2
												}
											}
										}
									}
									ELEMENT[at0013] occurrences matches {0..1} matches {    -- Lymphatic invasion (L)
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0036,    -- LX
													at0037,    -- L0
													at0038]    -- L1
												}
											}
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {    -- Venous invasion (V)
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0039,    -- VX
													at0040,    -- V0
													at0041,    -- V1
													at0042]    -- V2
												}
											}
										}
									}
									ELEMENT[at0043] occurrences matches {0..1} matches {    -- Perineural invasion (Pn)
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0044,    -- PnX
													at0045,    -- Pn0
													at0046]    -- Pn1
												}
											}
										}
									}
									ELEMENT[at0018] occurrences matches {0..2} matches {    -- Multiple primary tumours (m)
										value matches {
											DV_TEXT matches {*}
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0029,    -- m
													at0030]    -- is
												}
											}
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {    -- Multimodality therapy (y)
										value matches {
											DV_BOOLEAN matches {*}
										}
									}
									ELEMENT[at0020] occurrences matches {0..1} matches {    -- Recurrent (r)
										value matches {
											DV_BOOLEAN matches {*}
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {    -- Autopsy (a)
										value matches {
											DV_BOOLEAN matches {*}
										}
									}
									ELEMENT[at0015] occurrences matches {0..*} matches {    -- Other descriptor
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {    -- TNM assessment
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {    -- Stage grouping
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0023] occurrences matches {0..*} matches {    -- TNM Edition
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0022] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"TNM stage - clinical">
					description = <"A framework for the pretreatment clinical classification and grading of malignancies using the TNM system.">
					comment = <"Designated as TNM or cTNM.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Point in time event">
					description = <"Default, unspecified point in time which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0005"] = <
					text = <"Cancer type">
					description = <"The type of cancer being assessed.">
					comment = <"Coding of the type of the cancer with a terminology is strongly preferred.">
				>
				["at0006"] = <
					text = <"Anatomical site">
					description = <"The anatomical site where the assessed cancer is situated.">
				>
				["at0007"] = <
					text = <"Anatomical subsite">
					description = <"The anatomical subsite where the assessed cancer is situated.">
				>
				["at0008"] = <
					text = <"Primary tumour (T)">
					description = <"Assessment of the primary tumour.">
					comment = <"Designated as 'T' or 'cT'. Occurrences (0..2) allows representation of both the primary tumour (T) assessment plus an optional certainty factor (C) assessment. For example: 'T1'; or 'T3C2'.">
				>
				["at0009"] = <
					text = <"Regional lymph nodes (N)">
					description = <"Assessment of the regional lymph nodes.">
					comment = <"Designated as 'N' or 'cN'. Occurrences (0..2) allows representation of both the regional lymph node (N) assessment plus an optional certainty factor (C) assessment. For example: 'NX'; 'N2C1'.">
				>
				["at0010"] = <
					text = <"Distant metastasis (M)">
					description = <"Assessment of distant metastasis.">
					comment = <"Designated as 'M' or 'cM'. Occurrences (0..2) allows representation of both the distant metastases (M) assessment plus an optional certainty factor (C) assessment. For example: 'M1'; or 'M0C2'.">
				>
				["at0011"] = <
					text = <"Histopathological grading (G)">
					description = <"Histopathological grading of the tumour.">
					comment = <"For example: 'GX' (Grade of differentiation cannot be assessed); or 'G4' (Undifferentiated). A free text option has been allowed to cater for additional representations such as: Grades 3 and 4 can be combined in some circumstances as 'G3-4, Poorly differentiated or undifferentiated'; the bone and soft tissue sarcoma classifications also use 'high grade' and 'low grade'; and special systems of grading are recommended for tumours of breast, corpus uteri, and liver.">
				>
				["at0012"] = <
					text = <"Residual tumour (R)">
					description = <"Assessment of the presence of residual tumour after treatment.">
					comment = <"For example: 'R1 (Microscopic residual tumour)'.">
				>
				["at0013"] = <
					text = <"Lymphatic invasion (L)">
					description = <"Assessment of invasion into the lymphatic system.">
					comment = <"For example: 'L0 (No lymphatic invasion)'.">
				>
				["at0014"] = <
					text = <"Venous invasion (V)">
					description = <"Assessment of invasion into the venous system.">
					comment = <"For example: 'V1 (Microscopic venous invasion)'.">
				>
				["at0015"] = <
					text = <"Other descriptor">
					description = <"Any other descriptor used in the assessment.">
				>
				["at0016"] = <
					text = <"TNM assessment">
					description = <"Concatenation of 'T', 'N' and 'M' assessments plus any optional assessments of 'G', 'R', 'L', 'V', prefixes and/or suffixes, as applicable.">
				>
				["at0017"] = <
					text = <"Stage grouping">
					description = <"The staging of the TNM assessment.">
				>
				["at0020"] = <
					text = <"Recurrent (r)">
					description = <"Assessment is performed for a recurring cancer after a disease-free interval.">
					comment = <"Designated as the prefix 'r'.">
				>
				["at0019"] = <
					text = <"Multimodality therapy (y)">
					description = <"Assessment is performed during or following initial multimodality therapy.">
					comment = <"Designated as the prefix 'y'.">
				>
				["at0021"] = <
					text = <"Autopsy (a)">
					description = <"Assessment is performed by postmortem examination after the death of the patient.">
					comment = <"Designated as the prefix 'a'.">
				>
				["at0023"] = <
					text = <"TNM Edition">
					description = <"The edition of the TNM classification system used for the assessment.">
				>
				["at0022"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0018"] = <
					text = <"Multiple primary tumours (m)">
					description = <"Presence of multiple primary tumours at a single site.">
					comment = <"Designated as a suffix for the Primary Tumour as the letters 'm', 's' or a numeric. Occurrences (0..3) allows representation of one of each of the optional 3 suffixes, if required.
Multiple synchronous tumours in one organ may be:
a.	Multiple noninvasive tumours; 
b.	Multiple invasive tumours; 
c.	Multiple invasive tumours with associated carcinoma in situ;  or
d.	A single invasive tumour with associated carcinoma in situ
For (a) the multiplicity should be indicated by the suffix \"(m)\", e.g. Tis(m).
For (b) and (c) the tumour with the highest T category is classified and the multiplicity or the number of invasive tumours is indicated in parentheses, e.g., T2(m) or T2(4).
For (c) and (d) the presence of associated carcinoma in situ may be indicated by the suffix \"(is)\", e.g., T3(m, is) or T2(3, is) or T2(is).">
				>
				["at0029"] = <
					text = <"m">
					description = <"Multiple simultaneous tumours in one organ.">
				>
				["at0030"] = <
					text = <"is">
					description = <"Presence of associated carcinoma in situ.">
				>
				["at0024"] = <
					text = <"C1">
					description = <"Evidence from standard diagnostic means (e.g., inspection, palpation, and standard radiography, intraluminal endoscopy for tumours of certain organs).">
				>
				["at0025"] = <
					text = <"C2">
					description = <"Evidence obtained by special diagnostic means (e.g., radiographic imaging in special projections, tomography, computerized tomography [CT], ultrasonography, lymphography, angiography; scintigraphy; magnetic resonance imaging [MRI]; endoscopy, biopsy, and cytology).">
				>
				["at0026"] = <
					text = <"C3">
					description = <"Evidence from surgical exploration, including biopsy and cytology.">
				>
				["at0027"] = <
					text = <"C4">
					description = <"Evidence of the extent of disease following definitive surgery and pathological examination of the resected specimen.">
				>
				["at0028"] = <
					text = <"C5">
					description = <"Evidence from autopsy.">
				>
				["at0031"] = <
					text = <"GX">
					description = <"Grade of differentiation cannot be assessed.">
				>
				["at0032"] = <
					text = <"G1">
					description = <"Well differentiated.">
				>
				["at0033"] = <
					text = <"G2">
					description = <"Moderately differentiated.">
				>
				["at0034"] = <
					text = <"G3">
					description = <"Poorly differentiated.">
				>
				["at0035"] = <
					text = <"G4">
					description = <"Undifferentiated.">
				>
				["at0036"] = <
					text = <"LX">
					description = <"Lymphatic invasion cannot be assessed.">
				>
				["at0037"] = <
					text = <"L0">
					description = <"No lymphatic invasion.">
				>
				["at0038"] = <
					text = <"L1">
					description = <"Lymphatic invasion.">
				>
				["at0039"] = <
					text = <"VX">
					description = <"Venous invasion cannot be assessed.">
				>
				["at0040"] = <
					text = <"V0">
					description = <"No venous invasion.">
				>
				["at0041"] = <
					text = <"V1">
					description = <"Microscopic venous invasion.">
				>
				["at0042"] = <
					text = <"V2">
					description = <"Macroscopic venous invasion.">
				>
				["at0043"] = <
					text = <"Perineural invasion (Pn)">
					description = <"Assessment of invasion into the space surrounding nerves.">
					comment = <"For example: 'Pn0 (No perineural invasion)'.">
				>
				["at0044"] = <
					text = <"PnX">
					description = <"Perineural invasion cannot be assessed.">
				>
				["at0045"] = <
					text = <"Pn0">
					description = <"No perineural invasion.">
				>
				["at0046"] = <
					text = <"Pn1">
					description = <"Perineural invasion.">
				>
				["at0047"] = <
					text = <"RX">
					description = <"Presence of residual tumour cannot be assessed.">
				>
				["at0048"] = <
					text = <"R0">
					description = <"No residual tumour.">
				>
				["at0049"] = <
					text = <"R1">
					description = <"Microscopic residual tumour.">
				>
				["at0050"] = <
					text = <"R2">
					description = <"Macroscopic residual tumour.">
				>
			>
		>
	>
